plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'jacoco'
	id 'java'
	id 'war'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.calculi'
version = '1.0.0'
sourceCompatibility = '12'

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

test {
	reports {
		junitXml.enabled = true
	}
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.4"
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.destination file("${buildDir}/jacocoHtml")
	}
}

repositories {
	mavenCentral()
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	reports.junitXml.enabled = true
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestCompile 'junit:junit:4.12'
}
